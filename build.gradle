buildscript {
    ext.springBootVersion = '2.2.6.RELEASE'
    ext.springCloudVersion = 'Hoxton.SR4'

    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath 'gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:2.2.2'
        classpath 'com.bmuschko:gradle-docker-plugin:6.4.0'
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'org.ajoberstar.grgit'
apply from: 'gradle/dependency-locking.gradle'
apply from: 'gradle/runtime.gradle'
apply from: 'gradle/docker.gradle'

java {
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

repositories {
    mavenCentral()
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' } // for springfox-swagger 3.+
}
dependencies {
    ['annotationProcessor', 'compileOnly', 'implementation', 'runtimeOnly', 'testImplementation'].each { conf ->
        dependencies.add(conf, platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
        dependencies.add(conf, platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"))
    }
    implementation platform('io.projectreactor:reactor-bom:Dysprosium-SR7')
    testImplementation platform('org.junit:junit-bom:5.+')

    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.data:spring-data-commons'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'io.github.openfeign:feign-okhttp'

    implementation 'io.springfox:springfox-swagger2:3.+'
    implementation 'io.springfox:springfox-swagger-ui:3.+'
    implementation 'io.springfox:springfox-spring-webmvc:3.+'

    implementation 'com.stoyanr:evictor:1.+'
    implementation 'org.ow2.asm:asm:7.+'

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

task cleanAll {
    dependsOn clean, cleanIdea, cleanIdeaWorkspace
    doLast {
        delete 'out', '.idea', 'logs'
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = new groovy.json.JsonSlurper().parseText('https://services.gradle.org/versions/current'.toURL().text).version
}
