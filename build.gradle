buildscript {
    ext.springBootVersion = '2.4.4'
    ext.springCloudVersion = '2020.0.2'

    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
//        classpath 'com.gorylenko.gradle-git-properties:gradle-git-properties:2.3.0'
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.0'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
//apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'org.ajoberstar.grgit'
apply from: 'gradle/dependency-locking.gradle'
apply from: 'gradle/runtime.gradle'
apply from: 'gradle/docker.gradle'

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

repositories {
    mavenCentral()
}
dependencies {
    ['annotationProcessor', 'compileOnly', 'implementation', 'runtimeOnly', 'testImplementation'].each { conf ->
        dependencies.add(conf, platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
        dependencies.add(conf, platform("org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"))
    }
    implementation platform('io.projectreactor:reactor-bom:2020.0.5')
    testImplementation platform('org.junit:junit-bom:5.+')

    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    compileOnly 'org.projectlombok:lombok:1.18.20'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.data:spring-data-commons'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation 'io.github.openfeign:feign-okhttp'
    implementation 'io.projectreactor:reactor-core'

    implementation 'io.springfox:springfox-boot-starter:3.+'
    implementation 'io.springfox:springfox-swagger-ui:3.+'

    implementation 'org.ow2.asm:asm:7.+'

    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

processResources {
    filesMatching('build.yml') { it.expand(project.properties) }
}

test {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

task cleanAll {
    dependsOn clean, cleanIdea, cleanIdeaWorkspace
    doLast {
        delete 'out', '.idea', 'logs'
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
//    gradleVersion = new groovy.json.JsonSlurper().parseText('https://services.gradle.org/versions/current'.toURL().text).version
}
